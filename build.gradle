// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'gradle.plugin.gradle-plugins:jartest:1.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'idea'


allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configure([project(':tapstream-core'), project(':tapstream-android')]) {
    def sonatypeUser = project.properties['sonatypeUser']
    def sonatypePass = project.properties['sonatypePass']
    def repoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    def siteUrl = 'https://tapstream.com/developer/android/integration/'
    def gitUrl = 'https://github.com/tapstream/tapstream-sdk-android.git'
    def gitConn = 'scm:git:git@github.com:tapstream/tapstream-sdk.git'

    apply plugin: 'maven'
    apply plugin: 'signing'

    afterEvaluate {project ->

        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: repoUrl) {
                        authentication(userName: sonatypeUser, password: sonatypePass)
                    }

                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    pom.project {
                        name project.pomName
                        packaging project.pomPackaging
                        description project.pomDescription
                        url siteUrl

                        organization {
                            name 'Tapstream'
                            url 'https://tapstream.com'
                        }

                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://www.opensource.org/licenses/mit-license.php'
                            }
                        }
                        developers {
                            developer {
                                id = 'adambard'
                                name = 'Adam Bard'
                                organization = 'Tapstream'
                                organizationUrl = 'https://tapstream.com'
                            }
                            developer {
                                id = 'nsitarz'
                                name = 'Nick Sitarz'
                                organization = 'Tapstream'
                                organizationUrl = 'https://tapstream.com'
                            }
                        }
                        scm {
                            connection gitUrl
                            developerConnection gitConn
                            url gitConn

                        }
                    }

                }
            }
        }

        signing {
            required { gradle.taskGraph.hasTask("uploadArchives") }
            sign configurations.archives
        }
    }
}

