apply plugin: 'java'

configurations {
    testArtifacts.extendsFrom testCompile
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.nanohttpd:nanohttpd:2.3.0'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile 'com.google.guava:guava:19.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.json:json:20131018'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into sourceSets.test.output.classesDir
}

task testJar (type: Jar, dependsOn: testClasses) {
    classifier "test"
    from sourceSets.test.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    testArtifacts testJar
    archives sourcesJar, javadocJar
}

ext {
    deployable = true
    pomPackaging = 'jar'
    pomName = 'Tapstream Core Java SDK'
    pomDescription = 'This library contains the building blocks for a Tapstream Java SDK'
}
